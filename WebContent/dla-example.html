<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="utf-8">
<meta name="viewport" content="width=device-width, initial-scale=1">
<meta name="keywords" content="WebGL, shader, three.js, javaScript, example, 3D, simulation">
<meta name="description" content="dla shader example">
<meta name="author" content="Javier Gracia Carpio">

<title>dla shader example</title>

<link rel="icon" type="image/x-icon" href="/favicon.ico">

<!-- CSS files -->
<link rel="stylesheet" href="/css/styles.css">

<!-- JavaScript files -->
<script type="text/javascript" src="/js/libs/three.min.js"></script>
<script type="text/javascript" src="/js/libs/stats.min.js"></script>
<script type="text/javascript" src="/js/libs/dat.gui.min.js"></script>
<script type="text/javascript" src="/js/libs/OrbitControls.js"></script>
<script type="text/javascript" src="/js/libs/GPUComputationRenderer.js"></script>
</head>

<body>
	<!-- Navigation bar -->
	<header class="nav-container">
		<h1 class="nav-header">
			<a href="/index.html" class="nav-item is-header-item">WebGL-shaders</a>
		</h1>

		<nav class="nav-menu-wrapper">
			<span class="nav-item is-menu-item menu-icon" onclick="return false;"></span>

			<ul class="nav-menu-list">
				<li><a href="/index.html" class="nav-item">Back to list</a></li>
				<li><a href="/js/shader-example-dla.js" class="nav-item">JavaScript file</a></li>
				<li><a href="/shaders/frag-dla-pos.glsl" class="nav-item">Position shader</a></li>
				<li><a href="/shaders/frag-dla-vel.glsl" class="nav-item">Velocity shader</a></li>
				<li><a href="/shaders/vert-dla.glsl" class="nav-item">Vertex shader</a></li>
				<li><a href="/shaders/frag-dla.glsl" class="nav-item">Fragment shader</a></li>
			</ul>

			<ul class="nav-menu-list">
				<li><a href="/about.html" class="nav-item">About</a></li>
				<li><a href="https://github.com/jagracar/webgl-shader-examples" class="nav-item"><span class="github-icon"></span>
						Github</a></li>
			</ul>
		</nav>
	</header>

	<!-- Main content -->
	<main class="main-container">
	<article class="content">
		<div class="sketch-container" id="sketch-container">
			<div class="sketch-gui" id="sketch-gui"></div>
			<div class="sketch-stats" id="sketch-stats"></div>
		</div>

		<script type="x-shader/x-fragment" id="positionShader">
#define GLSLIFY 1
/*
 * Random number generator with a vec2 seed
 *
 * Credits:
 * http://byteblacksmith.com/improvements-to-the-canonical-one-liner-glsl-rand-for-opengl-es-2-0
 * https://github.com/mattdesl/glsl-random
 */
highp float random2d(vec2 co) {
    highp float a = 12.9898;
    highp float b = 78.233;
    highp float c = 43758.5453;
    highp float dt = dot(co.xy, vec2(a, b));
    highp float sn = mod(dt, 3.14);
    return fract(sin(sn) * c);
}

// Simulation uniforms
uniform float u_minDistance;
uniform float u_maxDistance;
uniform float u_time;

// Simulation constants
const float width = resolution.x;
const float height = resolution.y;
const float nParticles = width * height;

/*
 * The main program
 */
void main() {
    // Get the particle texture position
    vec2 uv = gl_FragCoord.xy / resolution;

    // Get the particle current position
    vec4 position = texture2D(u_positionTexture, uv);

    // Update the particle position if it has not been aggregated
    if (position.w > 0.0) {
        // Move the particle to a new random position
        float ang = 2.0 * 3.141593 * random2d(123.456 * position.xy + u_time);
        position.xy += 0.9 * u_minDistance * vec2(cos(ang), sin(ang));

        // Loop over all the particles in the simulation
        for (float i = 0.0; i < nParticles; i++) {
            // Get the particle position and velocity
            vec2 particleUv = vec2(mod(i, width) + 0.5, floor(i / width) + 0.5) / resolution;
            vec4 particlePosition = texture2D(u_positionTexture, particleUv);

            // Check if it's an aggregated particle
            if (position.w > 0.0 && particlePosition.w < 0.0) {
                // Calculate the distance between the two particles
                float distance = length(particlePosition.xy - position.xy);

                // Set the particle as aggregated if the distance is small
                // enough and we are not comparing the particle with itself
                if (distance != 0.0 && distance < u_minDistance) {
                    position.w = -1.0;
                    position.xy = particlePosition.xy + u_minDistance * (position.xy - particlePosition.xy) / distance;
                    break;
                }
            }
        }

        // Make sure that the particle distance to the center is smaller than
        // the maximum allowed distance
        float distanceToCenter = length(position.xy);

        if (distanceToCenter > u_maxDistance) {
            position.xy -= 2.0 * u_maxDistance * position.xy / distanceToCenter;
        }
    }

    // Return the updated particle position
    gl_FragColor = position;
}

		</script>

		<script type="x-shader/x-fragment" id="velocityShader">
#define GLSLIFY 1

		</script>

		<script type="x-shader/x-vertex" id="vertexShader">
#define GLSLIFY 1
// Particle index attribute
attribute float a_index;

// Simulation uniforms
uniform float u_width;
uniform float u_height;
uniform float u_particleSize;
uniform sampler2D u_positionTexture;

// Varying with the aggregation information
varying float v_aggregation;

/*
 * The main program
 */
void main() {
    // Get the particle model view position
    vec2 uv = vec2((mod(a_index, u_width) + 0.5) / u_width, (floor(a_index / u_width) + 0.5) / u_height);
    vec4 position = texture2D(u_positionTexture, uv);
    vec4 mvPosition = modelViewMatrix * vec4(position.xyz, 1.0);

    // Pass the aggregation information to the fragment shader
    v_aggregation = position.w;

    // Vertex shader output
    gl_PointSize = v_aggregation < 0.0 ? -u_particleSize / mvPosition.z : -0.5 * u_particleSize / mvPosition.z;
    gl_Position = projectionMatrix * mvPosition;
}

		</script>

		<script type="x-shader/x-fragment" id="fragmentShader">
#define GLSLIFY 1
// Texture with the particle profile
uniform sampler2D u_texture;

// Varying with the aggregation information
varying float v_aggregation;

/*
 * The main program
 */
void main() {
    // Use a different color for aggregated and non-aggregated particles
    vec3 particleColor = v_aggregation < 0.0 ? vec3(1.0) : vec3(0.5);

    // Fragment shader output
    gl_FragColor = vec4(particleColor, texture2D(u_texture, gl_PointCoord).a);
}

		</script>

		<script type="text/javascript" src="/js/shader-example-dla.js"></script>
	</article>
	</main>
</body>
</html>